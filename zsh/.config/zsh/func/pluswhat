# Description: A better and recursive which/whence - traces command resolution
# Usage: pluswhat <command> [command...]
# From: https://leahneukirchen.org/dotfiles/.zshrc

pluswhat() {
    emulate -L zsh
    
    ( # constrain unalias to subshell
        local cmd words
        for cmd; do
            if (( $+aliases[$cmd] )); then
                printf '%s: aliased to %s\n' $cmd $aliases[$cmd]
                local -a words=(${${(z)aliases[$cmd]}:#(*=*|rlwrap|noglob|command)})
                unalias $cmd
                if [[ $words[1] == '\'* ]]; then
                    words[1]=${words[1]#'\'}
                    unalias $words[1] 2>/dev/null
                fi
                pluswhat $words[1]
            elif (( $+functions[$cmd] )); then
                whence -v $cmd
                whence -f $cmd
            elif (( $+commands[$cmd] )); then
                pluswhat $commands[$cmd]
            elif [[ -h $cmd ]]; then
                file $cmd
                pluswhat $cmd:A
            elif [[ -x $cmd ]]; then
                file $cmd
            else
                which $cmd
            fi
        done
    )
}

# Enable completion
compdef pluswhat=which